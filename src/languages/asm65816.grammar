@top Program { line* }

@skip { Comment | space }

line {
  Instruction (newline | eof) | newline
}

Instruction { Opcode mode? }

@tokens {
  space { $[ \t]+ }
  newline { $[\n\r] }

  Comment { ";" ![\n]* }

  b { $[01] }
  d { $[0-9] }
  h { $[0-9A-Fa-f] }

  byteBin { "%" b b b b b b b b }
  byteDec { d | d d | d d d }
  byteHex { "$" h h }

  wordBin { "%" b b b b b b b b b b b b b b b b }
  wordDec { d d d d | d d d d d }
  wordHex { "$" h h h h }

  longBin { "%" b b b b b b b b b b b b b b b b b b b b b b b b }
  longDec { d d d d d d | d d d d d d d | d d d d d d d d }
  longHex { "$" h h h h h h }

  Byte { byteBin | byteHex | byteDec }
  Word { wordBin | wordHex | wordDec }
  Long { longBin | longHex | longDec }

  ConstByte { "#" byteBin | "#" byteHex | "#" byteDec }
  ConstWord { "#" wordBin | "#" wordHex | "#" wordDec }

  A { $[aA] }

  X { "," $[xX] }
  Y { "," $[yY] }
  S { "," $[sS] }

  "(" ")" "[" "]" ","

  eof { @eof }

  Opcode {
    "ADC" | "AND" | "ASL" | "BCC" | "BCS" | "BEQ" | "BIT" | "BMI" | "BNE" | "BPL"
  | "BRA" | "BRK" | "BRL" | "BVC" | "BVS" | "CLC" | "CLD" | "CLI" | "CLV" | "CMP"
  | "COP" | "CPX" | "CPY" | "DEC" | "DEX" | "DEY" | "EOR" | "INC" | "INX" | "INY"
  | "JMP" | "JML" | "JSL" | "JSR" | "LDA" | "LDX" | "LDY" | "LSR" | "MVN" | "MVP"
  | "NOP" | "ORA" | "PEA" | "PEI" | "PER" | "PHA" | "PHB" | "PHD" | "PHK" | "PHP"
  | "PHX" | "PHY" | "PLA" | "PLB" | "PLD" | "PLP" | "PLX" | "PLY" | "REP" | "ROL"
  | "ROR" | "RTI" | "RTL" | "RTS" | "SBC" | "SEC" | "SED" | "SEI" | "SEP" | "STA"
  | "STP" | "STX" | "STY" | "STZ" | "TAX" | "TAY" | "TCD" | "TCS" | "TDC" | "TRB"
  | "TSB" | "TSC" | "TSX" | "TXA" | "TXS" | "TXY" | "TYA" | "TYX" | "WAI" | "WDM"
  | "XBA" | "XCE"
    "adc" | "and" | "asl" | "bcc" | "bcs" | "beq" | "bit" | "bmi" | "bne" | "bpl"
  | "bra" | "brk" | "brl" | "bvc" | "bvs" | "clc" | "cld" | "cli" | "clv" | "cmp"
  | "cop" | "cpx" | "cpy" | "dec" | "dex" | "dey" | "eor" | "inc" | "inx" | "iny"
  | "jmp" | "jml" | "jsl" | "jsr" | "lda" | "ldx" | "ldy" | "lsr" | "mvn" | "mvp"
  | "nop" | "ora" | "pea" | "pei" | "per" | "pha" | "phb" | "phd" | "phk" | "php"
  | "phx" | "phy" | "pla" | "plb" | "pld" | "plp" | "plx" | "ply" | "rep" | "rol"
  | "ror" | "rti" | "rtl" | "rts" | "sbc" | "sec" | "sed" | "sei" | "sep" | "sta"
  | "stp" | "stx" | "sty" | "stz" | "tax" | "tay" | "tcd" | "tcs" | "tdc" | "trb"
  | "tsb" | "tsc" | "tsx" | "txa" | "txs" | "txy" | "tya" | "tyx" | "wai" | "wdm"
  | "xba" | "xce"
  }
}

Mode_Absolute                 { Word }
Mode_AbsoluteLong             { Long }
Mode_AbsoluteLong_X           { Long X }
Mode_Absolute_Indirect        { "(" Word ")" }
Mode_Absolute_IndirectLong    { "[" Word "]" }
Mode_Absolute_X               { Word X }
Mode_Absolute_X_Indirect      { "(" Word X ")" }
Mode_Absolute_Y               { Word Y }
Mode_Accumulator              { A }
Mode_BlockMove                { Byte "," Byte }
Mode_Direct                   { Byte }
Mode_Direct_Indirect          { "(" Byte ")" }
Mode_Direct_IndirectLong      { "[" Byte "]" }
Mode_Direct_IndirectLong_Y    { "[" Byte "]" Y }
Mode_Direct_Indirect_Y        { "(" Byte ")" Y }
Mode_Direct_X                 { Byte X }
Mode_Direct_X_Indirect        { "(" Byte X ")" }
Mode_Direct_Y                 { Byte Y }
Mode_Immediate                { ConstByte | ConstWord }
Mode_StackRelative            { Byte S }
Mode_StackRelative_Indirect_Y { "(" Byte S ")" Y }

mode {
  Mode_Absolute | Mode_AbsoluteLong | Mode_AbsoluteLong_X
| Mode_Absolute_Indirect | Mode_Absolute_IndirectLong | Mode_Absolute_X
| Mode_Absolute_X_Indirect | Mode_Absolute_Y | Mode_Accumulator | Mode_BlockMove
| Mode_Direct | Mode_Direct_Indirect | Mode_Direct_IndirectLong
| Mode_Direct_IndirectLong_Y | Mode_Direct_Indirect_Y | Mode_Direct_X
| Mode_Direct_X_Indirect | Mode_Direct_Y | Mode_Immediate | Mode_StackRelative
| Mode_StackRelative_Indirect_Y
}

@detectDelim
